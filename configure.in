dnl
dnl Configure template for G10
dnl
dnl (Process this file with autoconf to produce a configure script.)

AC_INIT(g10/g10.c)
AC_CONFIG_AUX_DIR(scripts)
AC_CONFIG_HEADER(config.h)

VERSION=0.0.0
PACKAGE=g10
AC_SUBST(VERSION)
AC_SUBST(PACKAGE)
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")

AC_ARG_ENABLE(m-debug,
[  --enable-m-debug    Enable debugging of memory allocation])
if test "$enableval" = y || test "$enableval" = yes; then
    AC_DEFINE(M_DEBUG)
fi
CFLAGS="-g"

dnl
AC_CANONICAL_SYSTEM
AC_MSG_CHECKING(cached information)
hostcheck="$target"
AC_CACHE_VAL(ac_cv_mpi_hostcheck, [ ac_cv_mpi_hostcheck="$hostcheck" ])
if test "$ac_cv_mpi_hostcheck" != "$hostcheck"; then
    AC_MSG_RESULT(changed)
    AC_MSG_WARN(config.cache exists!)
    AC_MSG_ERROR(you must do 'make distclean' first to compile for
different target or different parameters.)
else
    AC_MSG_RESULT(ok)
fi


dnl Checks for programs.

AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_INSTALL
AC_PROG_CC
AC_PROG_CPP

AC_ARG_PROGRAM


dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADERS(zlib.h,
		 [LIBS="$LIBS -lz"],
		 AC_MSG_WARN([zlib missing - creating without ZLIB support!])
		)


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strerror strtol strtoul)



dnl setup assembler stuff
if test -f ./mpi/config.links ; then
    . ./mpi/config.links
    AC_LINK_FILES( ${mpi_ln_src}, ${mpi_ln_dst} )
else
    AC_MSG_ERROR([mpi/config.links missing!])
fi



dnl checking wether we have the RSA source
dnl fixme: I found no way (aside of using Makefile.am.in)
dnl	   to add the requeired source int Makefile.am
dnl I used: add_cipher_SOURCES="rsa.c rsa.h"
dnl but of cource it can't work
AC_MSG_CHECKING(wether we have the rsa source)
if test -f cipher/rsa.c && test -f cipher/rsa.h; then
    AC_DEFINE(HAVE_RSA_CIPHER)
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
fi
AC_SUBST(add_cipher_SOURCES)

AC_OUTPUT([ Makefile scripts/Makefile util/Makefile mpi/Makefile \
	    mpi/generic/Makefile mpi/i386/Makefile \
	    cipher/Makefile \
	    include/Makefile \
	    g10/Makefile tools/Makefile ],
	  [echo timestamp > stamp-h ] )


